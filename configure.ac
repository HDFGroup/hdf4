dnl Process this file with autoconf to produce configure.
dnl
dnl
dnl Copyright by The HDF Group.
dnl Copyright by the Board of Trustees of the University of Illinois.
dnl All rights reserved.
dnl
dnl This file is part of HDF.  The full HDF copyright notice, including
dnl terms governing use, modification, and redistribution, is contained in
dnl the files COPYING and Copyright.html.  COPYING can be found at the root
dnl of the source code distribution tree; Copyright.html can be found at
dnl http://hdfgroup.org/products/hdf4/doc/Copyright.html.  If you do not have
dnl access to either file, you may request a copy from help@hdfgroup.org.
dnl 
dnl
AC_REVISION($Id$)

dnl ======================================================================
dnl Initialize configure.
dnl ======================================================================
dnl AC_INIT takes the name of the package, the version number, and an
dnl email address to report bugs. AC_CONFIG_SRCDIR takes a unique file
dnl as its argument.
dnl
dnl NOTE: Don't forget to change the version number here when we do a
dnl release!!!
dnl
AC_INIT([HDF], [4.2.7-post0], [help@hdfgroup.org])
AC_CONFIG_SRCDIR([hdf/src/atom.c])
AC_CONFIG_AUX_DIR([bin])
AC_CONFIG_HEADER([hdf/src/h4config.h])
AC_CONFIG_MACRO_DIR([m4])

AM_INIT_AUTOMAKE([foreign])
AM_MAINTAINER_MODE

AC_PREFIX_DEFAULT([`pwd`/hdf4])

dnl Run post processing on files created by configure.
dnl src/h4config.h:
dnl (This code is not correct. To be cleaned later.)
dnl libhdf4.settings:
dnl Remove all lines begun with "#" which are generated by CONDITIONAL's of
dnl configure.
AC_OUTPUT_COMMANDS([
  echo "creating hdf/src/h4config.h"
  sed 's/#define /#define H4_/' < hdf/src/h4config.h |\
    sed 's/#undef /#undef H4_/' > h4config
  if test ! -f hdf/src/h4config.h; then
    /bin/mv -f h4config hdf/src/h4config.h
  elif (diff h4config hdf/src/h4config.h >/dev/null); then
    /bin/rm -f h4config
    echo "hdf/src/h4config.h is unchanged"
  else
    /bin/mv -f h4config hdf/src/h4config.h
  fi
  echo "Post process libhdf4.settings"
  sed '/^#/d' < libhdf4.settings > libhdf4.settings.TMP
  cp libhdf4.settings.TMP libhdf4.settings
  rm -f libhdf4.settings.TMP
])

dnl ======================================================================
dnl Information on the package
dnl ======================================================================

dnl Dump all shell variables values.
AC_MSG_CHECKING([shell variables initial values])
set >&AS_MESSAGE_LOG_FD
AC_MSG_RESULT([done])

AC_CANONICAL_HOST

AC_MSG_CHECKING([if basename works])
BASENAME_TEST="`basename /foo/bar/baz/qux/basename_works`"
if test $BASENAME_TEST != "basename_works"; then
  AC_MSG_ERROR([basename program doesn't work])
else
  AC_MSG_RESULT([yes])
fi

AC_MSG_CHECKING([if xargs works])
XARGS_TEST="`echo /foo/bar/baz/qux/xargs_works | xargs basename`"
if test $XARGS_TEST != "xargs_works"; then
  AC_MSG_ERROR([xargs program doesn't work])
else
  AC_MSG_RESULT([yes])
fi

dnl Source any special files that we need. These files normally aren't
dnl present but can be used by the maintainers to fine tune things like
dnl turning on debug or profiling flags for the compiler. The search order
dnl is:
dnl
dnl         CPU-VENDOR-OS
dnl         VENDOR-OS
dnl         CPU-OS
dnl         CPU-VENDOR
dnl         OS
dnl         VENDOR
dnl         CPU
dnl
dnl If the `OS' ends with a version number then remove it. For instance,
dnl `freebsd3.1' would become `freebsd'
dnl
case "$host_os" in
  aix4.*)       host_os_novers="aix4.x"     ;;
  aix5.*)       host_os_novers="aix5.x"     ;;
  freebsd*)     host_os_novers="freebsd"    ;;
  irix5.*)      host_os_novers="irix5.x"    ;;
  irix6.*)      host_os_novers="irix6.x"    ;;
  osf4.*)       host_os_novers="osf4.x"     ;;
  osf5.*)       host_os_novers="osf5.x"     ;;
  solaris2.*)   host_os_novers="solaris2.x" ;;
  *)            host_os_novers="$host_os"   ;;
esac

host_config="none"
for f in $host_cpu-$host_vendor-$host_os        \
         $host_cpu-$host_vendor-$host_os_novers \
         $host_vendor-$host_os                  \
         $host_vendor-$host_os_novers           \
         $host_cpu-$host_os                     \
         $host_cpu-$host_os_novers              \
         $host_cpu-$host_vendor                 \
         $host_os                               \
         $host_os_novers                        \
         $host_vendor                           \
         $host_cpu ; do
  AC_MSG_CHECKING([for config $f])
  if test -f "$srcdir/config/$f"; then
    host_config=$srcdir/config/$f
    AC_MSG_RESULT([found])
    break
  fi
  AC_MSG_RESULT([no])
done
if test "X$host_config" != "Xnone"; then
  CC_BASENAME="`echo $CC | cut -f1 -d' ' | xargs basename 2>/dev/null`"
  F77_BASENAME="`echo $F77 | cut -f1 -d' ' | xargs basename 2>/dev/null`"
  CXX_BASENAME="`echo $CXX | cut -f1 -d' ' | xargs basename 2>/dev/null`"
  . $host_config
fi

dnl ======================================================================
dnl Checks for NetCDF-2.3.2 support 
dnl ======================================================================

AC_ARG_ENABLE([netcdf],
              [AC_HELP_STRING([--enable-netcdf],
                              [Build HDF4 versions of NetCDF APIs (version 2.3.2) [default=yes]])],,
              enableval="yes")

case "$enableval" in
  yes)
    BUILD_NETCDF="yes"
    AC_DEFINE([HAVE_NETCDF], [1], [Define if we support HDF NetCDF APIs version 2.3.2])
    ;;
  no)
    BUILD_NETCDF="no"
    ;;
esac
AM_CONDITIONAL([HDF_BUILD_NETCDF], [test "X$BUILD_NETCDF" = "Xyes"])
AC_SUBST([BUILD_NETCDF])

dnl ======================================================================
dnl Checks for programs
dnl ======================================================================

AC_PROG_MAKE_SET

AC_PROG_CC
AC_PROG_CPP
AC_PROG_CXX

AC_ARG_ENABLE([fortran],
              [AC_HELP_STRING([--enable-fortran],
                              [Build Fortran into library [default=yes]])],,
              enableval="yes")

case "$enableval" in
  yes)
    BUILD_FORTRAN="yes"
    AC_PROG_F77
    AC_F77_WRAPPERS

    if test "X$F77" = "X"; then
      BUILD_FORTRAN="no"
    fi
    ;;
  no)
    BUILD_FORTRAN="no"
    F77="no"
    ;;
esac
AM_CONDITIONAL([HDF_BUILD_FORTRAN], [test "X$BUILD_FORTRAN" = "Xyes"])
AC_SUBST([BUILD_FORTRAN])

dnl -------------------------------------------------------------------------
dnl Build static libraries by default. Furthermore, fortran shared libraries
dnl are unsupported. Disallow a user from enabling both shared libraries and
dnl fortran.
if test "X${enable_shared}" != "Xyes"; then
    enable_shared="no"
fi

if test "X${enable_shared}" = "Xyes"; then
    if test "X${BUILD_FORTRAN}" = "Xyes"; then
        AC_MSG_ERROR([Cannot build shared fortran libraries. Please configure with --disable-fortran flag.])
    fi
fi

AC_PROG_INSTALL
AC_PROG_LN_S

AC_CHECK_PROGS([AR], [ar xar], [:], [$PATH])

AC_CHECK_PROGS([YACC], ['bison -y' byacc yacc], [none], [])

if test "$YACC" = "none"; then
  AC_MSG_ERROR([cannot find yacc utility])
fi

AC_CHECK_PROGS([LEX], [flex lex], [none], [])

if test "$LEX" = "none"; then
  AC_MSG_ERROR([cannot find lex utility])
fi

AC_CHECK_PROG([DIFF],     [diff],     [diff -w])
AC_CHECK_PROG([MAKEINFO], [makeinfo], [makeinfo])
AC_CHECK_PROG([NEQN],     [neqn],     [neqn])
AC_CHECK_PROG([TBL],      [tbl],      [tbl])

AC_SUBST([AR])
AC_SUBST([DIFF])
AC_SUBST(STATIC_SHARED)
AC_SUBST([SHARED_EXTENSION])
AC_SUBST(enable_shared)
AC_SUBST(enable_static)
AC_SUBST(UNAME_INFO) UNAME_INFO=`uname -a`


dnl ======================================================================
dnl Checks for libraries
dnl ======================================================================

dnl ----------------------------------------------------------------------
dnl Fake --with-xxx option to allow us to create a help message for the
dnl following --with-xxx options which can take either a =DIR or =INC,LIB
dnl specifier.
dnl
AC_ARG_WITH([fnord],
  [
 For the following --with-xxx options, you can specify where the header
 files and libraries are in two different ways:

    --with-xxx=INC,LIB - Specify individually the include directory and
                         library directory separated by a comma
    --with-xxx=DIR     - Specify only the directory which contains the
                         include/ and lib/ subdirectories
  ])

dnl ----------------------------------------------------------------------
dnl Is the GNU zlib present? It has a header file `zlib.h' and a library
dnl `-lz' and their locations might be specified with the `--with-zlib'
dnl command-line switch. The value is an include path and/or a library path.
dnl If the library path is specified then it must be preceded by a comma.
AC_ARG_WITH([zlib],
            [AC_HELP_STRING([--with-zlib=DIR],
                            [Use zlib library [default=yes]])],,
            [withval=yes])

case "$withval" in
  yes)
    HAVE_ZLIB="yes"
    AC_CHECK_HEADERS([zlib.h],, [unset HAVE_ZLIB])
    AC_CHECK_LIB([z], [compress2],, [unset HAVE_ZLIB])

    if test -z "$HAVE_ZLIB"; then
      AC_MSG_ERROR([couldn't find zlib library])
    fi
    ;;
  no)
    AC_MSG_ERROR([zlib library required to build HDF4])
    ;;
  *)
    HAVE_ZLIB="yes"
    case "$withval" in
      *,*)
        zlib_inc="`echo $withval | cut -f1 -d,`"
        zlib_lib="`echo $withval | cut -f2 -d, -s`"
        ;;
      *)
        if test -n "$withval"; then
          zlib_inc="$withval/include"
          zlib_lib="$withval/lib"
        fi
        ;;
    esac

    dnl Trying to include -I/usr/include and -L/usr/lib is redundant and
    dnl can mess some compilers up.
    if test "X$zlib_inc" = "X/usr/include"; then
      zlib_inc=""
    fi
    if test "X$zlib_lib" = "X/usr/lib"; then
      zlib_lib=""
    fi

    if test -n "$zlib_inc"; then
      CPPFLAGS="$CPPFLAGS -I$zlib_inc"
    fi

    AC_CHECK_HEADERS([zlib.h],, [unset HAVE_ZLIB])

    if test -n "$zlib_lib"; then
      LDFLAGS="$LDFLAGS -L$zlib_lib"
    fi

    AC_CHECK_LIB([z], [compress2],, [unset HAVE_ZLIB])

    if test -z "$HAVE_ZLIB"; then
      AC_MSG_ERROR([couldn't find zlib library])
    fi
    ;;
esac

dnl ----------------------------------------------------------------------
dnl Is the JPEG library present?
AC_ARG_WITH([jpeg],
            [AC_HELP_STRING([--with-jpeg=DIR],
                            [Use jpeg library [default=yes]])],,
            [withval=yes])

case "$withval" in
  yes)
    HAVE_JPEG="yes"
    AC_CHECK_HEADERS([jpeglib.h],, [unset HAVE_JPEG])
    AC_CHECK_LIB([jpeg], [jpeg_start_decompress],, [unset HAVE_JPEG])

    if test -z "$HAVE_JPEG"; then
      AC_MSG_ERROR([couldn't find jpeg library])
    fi
    ;;
  no)
    AC_MSG_ERROR([jpeg library required to build HDF4])
    ;;
  *)
    HAVE_JPEG="yes"
    case "$withval" in
      *,*)
        jpeg_inc="`echo $withval | cut -f1 -d,`"
        jpeg_lib="`echo $withval | cut -f2 -d, -s`"
        ;;
      *)
        if test -n "$withval"; then
          jpeg_inc="$withval/include"
          jpeg_lib="$withval/lib"
        fi
        ;;
    esac

    dnl Trying to include -I/usr/include and -L/usr/lib is redundant and
    dnl can mess some compilers up.
    if test "X$jpeg_inc" = "X/usr/include"; then
      jpeg_inc=""
    fi
    if test "X$jpeg_lib" = "X/usr/lib"; then
      jpeg_lib=""
    fi

    if test -n "$jpeg_inc"; then
      CPPFLAGS="$CPPFLAGS -I$jpeg_inc"
    fi

    AC_CHECK_HEADERS([jpeglib.h],, [unset HAVE_JPEG])

    if test -n "$jpeg_lib"; then
      LDFLAGS="$LDFLAGS -L$jpeg_lib"
    fi

    AC_CHECK_LIB([jpeg], [jpeg_start_decompress],, [unset HAVE_JPEG])

    if test -z "$HAVE_JPEG"; then
      AC_MSG_ERROR([couldn't find jpeg library])
    fi
    ;;
esac

dnl ----------------------------------------------------------------------
dnl Is the szip library present?
AC_SUBST(USE_COMP_SZIP) USE_COMP_SZIP="no"
AC_ARG_WITH([szlib],
            [AC_HELP_STRING([--with-szlib=DIR],
                            [Use szlib library [default=no]])],,
            [withval=no])

case "$withval" in
  yes)
    HAVE_SZIP="yes"
    AC_CHECK_HEADERS([szlib.h],, [unset HAVE_SZIP])
    AC_CHECK_LIB([sz], [SZ_BufftoBuffCompress],, [unset HAVE_SZIP])

    if test -z "$HAVE_SZIP"; then
      AC_MSG_ERROR([couldn't find szlib library])
    fi
    ;;
  no)
    AC_MSG_CHECKING([for szlib])
    AC_MSG_RESULT([suppressed])
    SZIP_INFO="${SZIP_INFO}disabled"
    ;;
  *)
    HAVE_SZIP="yes"
    case "$withval" in
      *,*)
        szip_inc="`echo $withval | cut -f1 -d,`"
        szip_lib="`echo $withval | cut -f2 -d, -s`"
        ;;
      *)
        if test -n "$withval"; then
          szip_inc="$withval/include"
          szip_lib="$withval/lib"
        fi
        ;;
    esac

    dnl Trying to include -I/usr/include and -L/usr/lib is redundant and
    dnl can mess some compilers up.
    if test "X$szip_inc" = "X/usr/include"; then
      szip_inc=""
    fi
    if test "X$szip_lib" = "X/usr/lib"; then
      szip_lib=""
    fi

    if test -n "$szip_inc"; then
      CPPFLAGS="$CPPFLAGS -I$szip_inc"
    fi

    AC_CHECK_HEADERS([szlib.h],, [unset HAVE_SZIP])

    if test -n "$szip_lib"; then
      LDFLAGS="$LDFLAGS -L$szip_lib"
    fi

    AC_CHECK_LIB([sz], [SZ_BufftoBuffCompress],, [unset HAVE_SZIP])

    if test -z "$HAVE_SZIP"; then
      AC_MSG_ERROR([couldn't find szlib library])
    else
      USE_COMP_SZIP="yes"
    fi
    ;;
esac

dnl Check to see if SZIP has encoder 
if test "X$HAVE_SZIP" = "Xyes"; then
    dnl SZLIB library is available. Check if it can encode.
    AC_MSG_CHECKING([for szlib encoder])
    
    AC_TRY_RUN([
    #include <stdlib.h>
    #include <szlib.h>

    int main(void)
    {
        /* SZ_encoder_enabled returns 1 if encoder is present */
        if (SZ_encoder_enabled() == 1)
            exit(0);
        else
            exit(1);
    }
    ], [CAN_ENCODE="yes"], [CAN_ENCODE="no"],)

    dnl Report szip encoder test results
    if test "X$CAN_ENCODE" = "Xyes"; then
        AC_MSG_RESULT([yes])
    fi
    if test "X$CAN_ENCODE" = "Xno"; then
        AC_MSG_RESULT([no])
    fi

    dnl Add "szip" to external filter list
    if test "X$CAN_ENCODE" = "Xyes"; then
        if test "X$SZIP_INFO" != "X"; then
            SZIP_INFO="${SZIP_INFO}"
        fi
        SZIP_INFO="${SZIP_INFO}enabled with encoder"
    fi

    if test "X$CAN_ENCODE" = "Xno"; then
        if test "X$SZIP_INFO" != "X"; then
            SZIP_INFO="${SZIP_INFO}"
        fi
        SZIP_INFO="${SZIP_INFO}enabled with decoder only"
    fi

    dnl Create macro to specify if encoder is present
    if test "X$CAN_ENCODE" = "Xyes"; then
        AC_DEFINE(HAVE_SZIP_ENCODER, 1,
                [Define if szip has encoder])
        SZIP_HAS_ENCODER="yes"
    fi
fi
AC_SUBST([SZIP_INFO])


dnl ----------------------------------------------------------------------
dnl Is XDR support present? The TRY_LINK info was gotten from the
dnl mfhdf/libsrc/local_nc.c file.
AC_MSG_CHECKING([for xdr library support])

AC_ARG_ENABLE([hdf4-xdr],,,enableval="no")

case "$enableval" in
  yes)
    build_xdr="yes"
    AC_MSG_RESULT([using HDF4 XDR]);
    ;;
  no)
    ;;
esac

if test "X$build_xdr" != "Xyes"; then
    AC_TRY_LINK([
    #ifdef VMS
    #define STDC_INCLUDES
    #endif
    #ifdef __ultrix
    #define GCC_FIX
    #endif 
    #include <rpc/types.h>
    #ifdef __ultrix
    #undef GCC_FIX
    #endif
    #include <rpc/xdr.h>], [xdr_int],
                [AC_MSG_RESULT([yes]); build_xdr="no"],
                [AC_MSG_RESULT([no]); build_xdr="yes"])
fi

AM_CONDITIONAL([HDF_BUILD_XDR], [test "X$build_xdr" = "Xyes"])

dnl For Solaris and Fuji systems, add the -nsl for XDR support
case "$host" in
  *-solaris*|*-hp-hpux*|S370-fujitsu-sysv*)
    LIBS="$LIBS -lnsl"  ;;
  i686-pc-cygwin*)
    LIBS="$LIBS -lrpc"  ;;
  *) ;;
esac

dnl ======================================================================
dnl Checks for header files
dnl ======================================================================

dnl Always put the /usr/include/rpc directory first in the search list.
CPPFLAGS="-I/usr/include/rpc $CPPFLAGS"

dnl ----------------------------------------------------------------------
dnl We're trying to link against the rpc library when building on Cygwin,
dnl but we need to make sure that it is present on the system. Do that here.
case "$host" in
  i686-pc-cygwin*)
    HAVE_RPC="yes"
    AC_CHECK_HEADER([rpc.h],[:], [unset HAVE_RPC])
    AC_CHECK_LIB([rpc], [xdr_opaque],[:], [unset HAVE_RPC])
    if test -z "$HAVE_RPC"; then
      AC_MSG_ERROR([couldn't find rpc library])
    fi
    ;;
esac

AC_HEADER_STDC

dnl ======================================================================
dnl Checks for types
dnl ======================================================================

dnl ======================================================================
dnl Checks for structures
dnl ======================================================================

dnl ======================================================================
dnl Checks for compiler characteristics
dnl ======================================================================

AC_PROG_CC_C_O
AC_PROG_CXXCPP

if test "X$BUILD_FORTRAN" = "Xyes"; then
  AC_PROG_F77_C_O
fi

dnl This is a bit of a hack. The AC_CHECK_SIZEOF macro is supposed to
dnl #define a value in a header file. However, we don't use a generated
dnl header file. So I check the value left over from autoconf's test  is
dnl >= 8.
AC_CHECK_SIZEOF([int*])

if test $ac_cv_sizeof_intp -ge 8; then
  CPPFLAGS="$CPPFLAGS -DBIG_LONGS"

  dnl Define super secret special flag for AIX machines in 64-bit mode.
  case "${host}" in
    *-ibm-aix*) CPPFLAGS="$CPPFLAGS -DAIX5L64" ;;
    *)          ;;
  esac
fi

dnl ----------------------------------------------------------------------
dnl Set some variables for general configuration information to be saved
dnl and installed with the libraries.
dnl
 
dnl HDF4 version from the first line of the README.txt file.
H4_VERSION="`cut -d' ' -f3 $srcdir/README.txt | head -1`"
AC_SUBST([H4_VERSION])

dnl Configuration date
AC_SUBST([CONFIG_DATE]) CONFIG_DATE="`date`"

dnl User doing the configuration
AC_SUBST([CONFIG_USER]) CONFIG_USER="`whoami`@`hostname`"

dnl Configuration mode (production, development, etc)
AC_SUBST([CONFIG_MODE])

AC_C_BIGENDIAN([], [CPPFLAGS="$CPPFLAGS -DSWAP"])

dnl Are we building this in debug or production mode? (Remove the -g flag
dnl in production mode.)
AC_MSG_CHECKING(for build mode)
AC_ARG_ENABLE(production,
              [AC_HELP_STRING([--enable-production],
                              [Determines how to run the compiler.])])

case "X-$enable_production" in
  X-|X-yes)
    AC_MSG_RESULT([production])
    CONFIG_MODE=production
    dnl Remove the "-g" flag from compile line if it's in there.
    CFLAGS_temp=""
    if test -n "$CFLAGS"; then
      for d in $CFLAGS ; do
        if test "X$d" != "X-g"; then
          CFLAGS_temp="$CFLAGS_temp $d"
        fi
      done
      CFLAGS=$CFLAGS_temp
    fi

    FFLAGS_temp=""
    if test -n "$FFLAGS"; then
      for d in $FFLAGS ; do
        if test "X$d" != "X-g"; then
          FFLAGS_temp="$FFLAGS_temp $d"
        fi
      done
      FFLAGS=$FFLAGS_temp
    fi

    CXXFLAGS_temp=""
    if test -n "$CXXFLAGS"; then
      for d in $CXXFLAGS ; do
        if test "X$d" != "X-g"; then
          CXXFLAGS_temp="$CXXFLAGS_temp $d"
        fi
      done
      CXXFLAGS=$CXXFLAGS_temp
    fi

    CFLAGS="$CFLAGS $PROD_CFLAGS"
    CXXFLAGS="$CXXFLAGS $PROD_CXXFLAGS"
    FFLAGS="$FFLAGS $PROD_FFLAGS"
    CPPFLAGS="$CPPFLAGS $PROD_CPPFLAGS"
    ;;
  X-no)
    AC_MSG_RESULT([development])
    CONFIG_MODE=development
    CFLAGS="$CFLAGS $DEBUG_CFLAGS"
    FFLAGS="$FFLAGS $DEBUG_FFLAGS"
    CXXFLAGS="$CXXFLAGS $DEBUG_CXXFLAGS"
    CPPFLAGS="$CPPFLAGS $DEBUG_CPPFLAGS"
    ;;
  *)
    AC_MSG_RESULT([user-defined])
    ;;
esac

dnl ======================================================================
dnl Checks for library functions
dnl ======================================================================

AC_MSG_CHECKING([for math library support])
AC_TRY_LINK([#include <math.h>], [sinh(37.927)],
            [AC_MSG_RESULT([yes])],
            [AC_MSG_RESULT([no]); LIBS="$LIBS -lm"])

AC_CHECK_FUNCS(fork system vfork wait)

dnl ======================================================================
dnl Libtool initialization
dnl ======================================================================
LT_INIT(dlopen disable-shared) 
LT_OUTPUT

dnl ======================================================================
dnl Checks for system services
dnl ======================================================================

dnl Copy NetCDF header files.
dnl
dnl FIXME: This is code stolen^Wborrowed from the old HDF4 configure.
dnl These header files should probably be generated by autoconf instead
dnl of being copied depending upon the host.
case "$host" in
  alpha-*-*)            BAR="alpha"       ;;
  alphaev*-*-*)         BAR="alpha"       ;;
  mips*-dec-ultrix*)    BAR="dec"         ;;
  mips*-sgi-irix6.*)    BAR="irix6"       ;;
  mips*-sgi-irix6_32*)  BAR="irix32"      ;;
  mips*-sgi-irix5*)     BAR="irix5"       ;;
  mips*-sgi-irix4*)     BAR="irix4"       ;;
  *-linux*)             BAR="linux"       ;;
  *-freebsd*)           BAR="fbsd"        ;;
  ia64-*-*)             BAR="ia64"        ;;
  *-ibm-aix*)           BAR="aix"         ;;
  i386-*-solaris2*)     BAR="solarisx86"  ;;
  sparc64-*-solaris2*)  BAR="solaris64"   ;;
  *-*-solaris2*)        BAR="solaris"     ;;
  *-sun-*)              BAR="sun"         ;;
  *-hp-hpux*)           BAR="hpux"        ;;
  *-convex-bsd*)        BAR="convex"      ;;
  *-cray-unicos*)       BAR="unicos"      ;;
  t3*-cray-craympp)     BAR="t3e"         ;;
  S370-fujitsu-sysv*)   BAR="fujivp"      ;;
  *-mac-*)              BAR="mac"         ;;
  *-apple*)             BAR="apple"       ;;
  i686-pc-cygwin*)      BAR="linux"       ;;
  *)                    echo "*** unknown host $host!"; exit 1 ;;
esac
src_files="mfhdf/libsrc/config/netcdf-FOO.h:mfhdf/libsrc/netcdf.h
           mfhdf/fortran/config/jackets-FOO.c:mfhdf/fortran/jackets.c
           mfhdf/fortran/config/netcdf-FOO.inc:mfhdf/fortran/netcdf.inc
           mfhdf/fortran/config/ftest-FOO.f:mfhdf/fortran/ftest.f"
src_files="`echo $src_files | sed -e s/FOO/${BAR}/g`"

for config_file in $src_files; do
  src_file="${srcdir}/`echo $config_file | sed -e s/:.*$//`"
  target_file="`echo $config_file | sed -e s/^[[^:]]*://g`"

  if test ! -r ${src_file}; then
    echo '***' "${progname}: cannot copy file \"${target_file}\"," 1>&2                  
    echo '***' "since the file \"${src_file}\" does not exist." 1>&2
    exit 1
  fi

  ## this sed command emulates the dirname command
  dstdir=`echo "$target_file" | sed -e 's,[[^/]]*$,,;s,/$,,;s,^$,.,'`

  ## Create directory for the target_file if it's not there.
  if test ! -d $dstdir; then
    ${srcdir}/bin/mkinstalldirs $dstdir
  fi

  echo "Copying \"${src_file}\" to \"${target_file}\" ."
  cp ${src_file} ${target_file}

  if test ! -r ${target_file}; then
    echo '***' "${progname}: cannot copy file \"${target_file}\"," 1>&2                  
    exit 1
  fi
done
  if test "X$BUILD_NETCDF" != "Xyes"; then
  echo "Renaming \"mfhdf/libsrc/netcdf.h\" to \"mfhdf/libsrc/hdf4_netcdf.h\" ."
    mv mfhdf/libsrc/netcdf.h mfhdf/libsrc/hdf4_netcdf.h
  fi 

dnl ------------------------------------------------------------------------
dnl Check to see if libtool has enabled shared libraries. Set a conditional
dnl as some Makefiles will build based on availability of shared libraries.
if (./libtool --features | grep '^enable shared libraries' > /dev/null); then
  enable_shared=yes
else
  enable_shared=no
fi

dnl ------------------------------------------------------------------------
dnl Specify shared library extension the host machine should recognize.
case "$host_os" in
  darwin*)
      SHARED_EXTENSION="dylib"
      ;;
  *)
      SHARED_EXTENSION="so"
      ;;
esac

dnl We don't need to say when we're entering directories if we're using
dnl GNU make because make does it for us.
if test "X$GMAKE" = "Xyes"; then
  AC_SUBST([SETX]) SETX=":"
else
  AC_SUBST([SETX]) SETX="set -x"
fi

AM_CONDITIONAL([HDF_BUILD_SHARED], [test "X$enable_shared" = "Xyes"])

dnl Compiler with version information. This consists of the full path
dnl name of the compiler and the reported version number.
AC_SUBST([CC_VERSION])
dnl Strip anything that looks like a flag off of $CC
CC_NOFLAGS=`echo $CC | sed 's/ -.*//'`

if `echo $CC_NOFLAGS | grep ^/ >/dev/null 2>&1`; then
  CC_VERSION="$CC"
else
  CC_VERSION="$CC";
  for x in `echo $PATH | sed -e 's/:/ /g'`; do
    if test -x $x/$CC_NOFLAGS; then
      CC_VERSION="$x/$CC"
      break
    fi
  done
fi
dnl This part doesn't work yet since HDF4 config files do not contain
dnl information for cc_vendor and cc_version as HDF5 similar files do.
dnl Needs to be fixed EIP 2010-01-21
dnl if test -n "$cc_vendor" && test -n "$cc_version"; then
dnl  CC_VERSION="$CC_VERSION ($cc_vendor-$cc_version)"
dnl fi

if test -n "$cc_version_info"; then
  CC_VERSION="$CC_VERSION ( $cc_version_info)"
fi

dnl Fortran compiler with version information. This consists of the full path
dnl name of the compiler and the reported version number.
AC_SUBST([F77_VERSION])
dnl Strip anything that looks like a flag off of $F77
F77_NOFLAGS=`echo $F77 | sed 's/ -.*//'`

if `echo $F77_NOFLAGS | grep ^/ >/dev/null 2>&1`; then
  F77_VERSION="$F77"
else
  F77_VERSION="$F77";
  for x in `echo $PATH | sed -e 's/:/ /g'`; do
    if test -x $x/$F77_NOFLAGS; then
      F77_VERSION="$x/$F77"
      break
    fi
  done
fi
if test -n "$fc_version_info"; then
  F77_VERSION="$F77_VERSION ( $fc_version_info)"
fi


dnl This part doesn't work yet since HDF4 config files do not contain
dnl information for fortran_vendor and fortran_version. 
dnl Needs to be fixed EIP 2010-01-21
dnl if test -n "$fortran_vendor" && test -n "$fortran_version"; then
dnl   F77_VERSION="$F77_VERSION ($fortran_vendor-$fortran_version)"
dnl fi

AC_CONFIG_FILES([Makefile
                 libhdf4.settings
                 hdf/Makefile
                 hdf/examples/Makefile
                 hdf/fortran/Makefile
                 hdf/fortran/examples/Makefile
                 hdf/src/Makefile
                 hdf/test/Makefile
                 hdf/util/Makefile
                 hdf/util/h4cc
                 hdf/util/h4fc
                 hdf/util/h4redeploy
                 hdf/util/testutil.sh
                 man/Makefile
                 mfhdf/Makefile
                 mfhdf/dumper/Makefile
                 mfhdf/dumper/testhdp.sh
                 mfhdf/examples/Makefile
                 mfhdf/examples/testexamples.sh
                 mfhdf/fortran/Makefile
                 mfhdf/fortran/examples/Makefile
                 mfhdf/hdfimport/Makefile
                 mfhdf/hdfimport/testutil.sh
                 mfhdf/hdiff/Makefile
                 mfhdf/hrepack/Makefile
                 mfhdf/libsrc/Makefile
                 mfhdf/ncdump/Makefile
                 mfhdf/ncgen/Makefile
                 mfhdf/nctest/Makefile
                 mfhdf/test/Makefile
                 mfhdf/xdr/Makefile])

AC_OUTPUT

chmod 755 hdf/util/h4cc hdf/util/h4redeploy

if test "X$BUILD_FORTRAN" = "Xyes"; then
  chmod 755 hdf/util/h4fc
fi

dnl show the configure settings
cat libhdf4.settings

